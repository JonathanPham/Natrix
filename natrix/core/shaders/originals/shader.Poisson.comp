
#include "bgfx_compute.sh"

uniform uvec2 _Size;


BUFFER_RO(_Obstacles, vec2, 0);

BUFFER_RO(_Divergence, float, 1);

BUFFER_RO(_PressureIn, float, 2);

BUFFER_WR(_PressureOut, float, 3);


uvec4 GetNeighbours(ivec2 pos, ivec2 size)
{
    uvec4 result;
    int maxX = size.x - 1;
    int maxY = size.y - 1;
    result.x = uint(pos.y) * _Size.x + uint(clamp(pos.x - 1, 0, maxX));
    result.y = uint(pos.y) * _Size.x + uint(clamp(pos.x + 1, 0, maxX));
    result.z = uint(clamp(pos.y - 1, 0, maxY) * size.x + pos.x);
    result.w = uint(clamp(pos.y + 1, 0, maxY) * size.x + pos.x);
    return result;
}


#define GROUP_SIZE 512

NUM_THREADS(GROUP_SIZE, 1, 1)
void main()
{
    if (gl_GlobalInvocationID.x >= _Size.x || gl_GlobalInvocationID.y >= _Size.y)
    {
        return;
    }
    uint pos = gl_GlobalInvocationID.y * _Size.x + gl_GlobalInvocationID.x;
    float rbeta = 0.25f;
    uvec4 n = GetNeighbours(ivec2(gl_GlobalInvocationID.xy), ivec2(_Size));
    float p = _PressureIn[pos];
    vec2 obsL = _Obstacles[n.x];
    vec2 obsR = _Obstacles[n.y];
    vec2 obsB = _Obstacles[n.z];
    vec2 obsT = _Obstacles[n.w];
    float x1 = (obsL.x > 0.0f || obsL.y > 0.0f) ? p : _PressureIn[n.x];
    float x2 = (obsR.x > 0.0f || obsR.y > 0.0f) ? p : _PressureIn[n.y];
    float y1 = (obsB.x > 0.0f || obsB.y > 0.0f) ? p : _PressureIn[n.z];
    float y2 = (obsT.x > 0.0f || obsT.y > 0.0f) ? p : _PressureIn[n.w];
    float b = _Divergence[pos];
    _PressureOut[pos] = (x1 + x2 + y1 + y2 - b) * rbeta;
}

